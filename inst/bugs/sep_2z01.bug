model
{
  K <-1000
  
	for(i in 1:n)
	{	   			
    d0[i]<- step(0.0001-y[i])    #d=1 if y=0
    d1[i]<- step(y[i]-0.9999)    #d=1 if y=1  
    random[i] <- inprod(alpha[EUID[i],],z[i,])

    # 1: mean
    logit(ph1[i,1])  <- inprod(b[], xmu.1[i,]) +random[i]*rid[1]
    cloglog(ph1[i,2])<- inprod(b[], xmu.1[i,]) +random[i]*rid[1]
    probit(ph1[i,3]) <- inprod(b[], xmu.1[i,]) +random[i]*rid[1]
    mu[i]<- inprod(ph1[i, ],link[1,])

    # 2: sum  
    log(den[i]) <- inprod(d[],  xsum.1[i,])+ random[i]*rid[2]

    s1[i]<- den[i]*mu[i]
    s2[i]<- den[i]*(1-mu[i])	

    # 3: zero
    logit(ph2[i,1])  <- inprod(b0[], x0.1[i,])  + random[i]*rid[3]
    cloglog(ph2[i,2])<- inprod(b0[], x0.1[i,])  + random[i]*rid[3]
    probit(ph2[i,3]) <- inprod(b0[], x0.1[i,])  + random[i]*rid[3]
    p0[i]<- inprod(ph2[i, ],link[2,])   

    # 4: one
    logit(ph3[i,1])  <- inprod(b1[], x1.1[i,])  + random[i]*rid[4]
    cloglog(ph3[i,2])<- inprod(b1[], x1.1[i,])  + random[i]*rid[4]
    probit(ph3[i,3]) <- inprod(b1[], x1.1[i,])  + random[i]*rid[4]
    p1[i]<- inprod(ph3[i, ],link[3,])   

    ll[i]<- d0[i]*log(p0[i])+(1-d0[i])*log(1-p0[i])+
             (1-d0[i])*d1[i]*log(p1[i])+(1-d0[i])*(1-d1[i])*log(1-p1[i])+ 
             (1-d0[i])*(1-d1[i])*(
                (s1[i]-1)*log(y[i])+(s2[i]-1)*log(1-y[i])+
                loggam(s1[i]+s2[i])-loggam(s1[i])-loggam(s2[i]))

    trick[i] <- K-ll[i]
    zero[i] ~ dpois(trick[i]) 

    ypred[i] <-(1-p0[i])*p1[i] + (1-p0[i])*(1-p1[i])*mu[i]
    # phi[i] <- den[i]
  }

  #################      regression coeff   ################## 
  tmp1 ~ dnorm(0, hyper[1,1])
  b[1] <- tmp1
  for(i in 1:(p.xmu-1))
  {
    ###   diffuse normal ###
    b.tmp[i,1] ~ dnorm(0.0, hyper[1,2]) 
     
    ###   L1 (lasso) ### 
		b.tmp[i,2] ~ dnorm(0.0,taub.L1[i]);  
    taub.L1[i] <- 1/sigmab.L1[i]; 	        
    sigmab.L1[i] ~ dexp(hyper[1,3]); 

    ###   L2  (ridge) ###
    b.tmp[i,3] ~ dnorm(0.0,taub.L2); 
		
		###   ARD  ###
		b.tmp[i,4] ~ dnorm(0.0,taub.ARD[i]);       
    taub.ARD[i] ~ dgamma(hyper[1,5], hyper[1,5]); 	      
    
    b[i+1] <- inprod(b.tmp[i, ],prior1[1,])   
 	}
  taub.L2 ~ dgamma(hyper[1,4],hyper[1,4]);  # L2 (ridge)
  
  	      
  tmp2 ~ dnorm(0, hyper[2,1])
  d[1] <- tmp2
	for(i in 1:(p.xsum-1))
	{
		d.tmp[i,1] ~ dnorm(0, hyper[2,2]) 
    
		d.tmp[i,2] ~ dnorm(0.0,taud.L1[i]);  
    taud.L1[i] <- 1/sigmad.L1[i]; 	        
    sigmad.L1[i] ~ dexp(hyper[2,3]); 
    
    d.tmp[i,3] ~ dnorm(0.0,taud.L2);
    		
		d.tmp[i,4] ~ dnorm(0.0,taud.ARD[i]);       
    taud.ARD[i] ~ dgamma(hyper[2,5], hyper[2,5]); 
    	          
    d[i+1] <- inprod(d.tmp[i, ],prior1[2,])
 	}
  taud.L2 ~ dgamma(hyper[2,4],hyper[2,4]);  
 

  tmp3~ dnorm(0, hyper[3,1])
  b0[1] <- tmp3
  for(i in 1:(p.x0-1))
	{
		b0.tmp[i,1] ~ dnorm(0, hyper[3,2]) 
    
		b0.tmp[i,2] ~ dnorm(0.0,taub0.L1[i]);  
    taub0.L1[i] <- 1/sigmab0.L1[i]; 	        
    sigmab0.L1[i] ~ dexp(hyper[3,3]); 

    b0.tmp[i,3] ~ dnorm(0.0,taub0.L2);
    
		b0.tmp[i,4] ~ dnorm(0.0,taub0.ARD[i]);       
    taub0.ARD[i] ~ dgamma(hyper[3,5],hyper[3,5]); 	 
         
    b0[i+1] <- inprod(b0.tmp[i, ],prior1[3,])   	
 	}
  taub0.L2 ~ dgamma(hyper[3,4],hyper[3,4]);  
  

  tmp4 ~ dnorm(0, hyper[4,1])	
  b1[1] <- tmp4
  for(i in 1:(p.x1-1))
	{
		b1.tmp[i,1] ~ dnorm(0, hyper[4,2]) 
		
		b1.tmp[i,2] ~ dnorm(0.0,taub1.L1[i]);  
    taub1.L1[i] <- 1/sigmab1.L1[i]; 	        
    sigmab1.L1[i] ~ dexp(hyper[4,3]); 
    
    b1.tmp[i,3] ~ dnorm(0.0,taub1.L2);

		b1.tmp[i,4] ~ dnorm(0.0,taub1.ARD[i]);       
    taub1.ARD[i] ~ dgamma(hyper[4,5],hyper[4,5]); 	 
             
    b1[i+1] <- inprod(b1.tmp[i, ],prior1[4, ])   	
 	}
  taub1.L2 ~ dgamma(hyper[4,4],hyper[4,4]);  
  

  for(i in 1:qz){ mu0[i] <- 0 }
  for(i in 1:nEU){ alpha[i,1:qz] ~ dmnorm(mu0,invSigma) }
  invSigma <- inverse(Sigma)
  Sigma <- Sigma.VC + Sigma.UN
  

  ######################### 1. VC #####################################################
  # if the zdummy is coded in a nested way, then the nested variance structure
  # (banded Compound symmetry) is just a special case of VC
  # in other words, whether CS or not is determined by how zdummy is coded.
  #####################################################################################
  for(i in 1:(qz-1)){
    for(j in (i+1):qz){ 
      Sigma.VC[i,j] <- 0
      Sigma.VC[j,i] <- 0} }
  
  for(i in 1:nz0){   
    sigma.VC1[i] ~ dunif(0, hyper2)       ##### uniform #####   
    
    t[i] ~ dt(0, pow(hyper2,-2), 1);           ##### half_Cauchy #####
    sigma.VC2[i] <- abs(t[i])
    
    for(j in (cumm[i]+1):(cumm[i]+m[i])) {
      Sigma.VC[j,j] <- pow(sigma.VC1[i],2)*prior2[1,1]+ 
                       pow(sigma.VC2[i],2)*prior2[1,2]    
    }  
  }   

  ######################### 2. UN (unstructed) #################################### 
  # used when nz0 = qz, such as continuous random variable with random coefficient
  #################################################################################

  rho1 ~ dunif(-0.999,0.999)
  rho2 ~ dunif(-0.999,0.999)
  l <- rho1*rho2 - sqrt( (1-pow(rho1,2))*(1-pow(rho2,2)))
  u <- rho1*rho2 + sqrt( (1-pow(rho1,2))*(1-pow(rho2,2)))
  rho3 ~ dunif(l, u)
  
  R[1,1] <- 1
  R[1,2] <- 0
  R[1,3] <- 0
  R[2,1] <- rho1
  R[2,2] <- sqrt(1-pow(rho1,2))
  R[2,3] <- 0
  R[3,1] <- rho2
  R[3,2] <- (rho3-rho1*rho2)/R[2,2]
  tmp <- abs(1-rho1^2-rho2^2-rho3^2+2*rho1*rho2*rho3)
  R[3,3] <- sqrt(tmp+0.0001)/R[2,2]

  ### uniform
  for(i in 1:qz){ 
    scale1[i] ~ dunif(0, hyper2)  
    for(j in 1:qz){ 
      V1[i,j] <- scale1[i]*equals(i,j)   
    }
  }
  V1R <- V1%*%R[1:qz,1:qz]
  Sigma.UN1 <- V1R%*%t(V1R)

  ###  half t    
  for(i in 1:qz){                        
    scale2[i] ~ dt(0, pow(hyper2,-2), 1);    
    for(j in 1:qz){ 
      V2[i,j] <- abs(scale2[i])*equals(i,j) 
    }
  }
  V2R <- V2%*%R[1:qz,1:qz]
  Sigma.UN2 <- V2R%*%t(V2R)

  Sigma.UN <- Sigma.UN1*prior2[2,1] +Sigma.UN2*prior2[2,2]   

}
